plugins {
    id 'java'
    id 'maven-publish'
    id 'org.ajoberstar.reckon' version '0.13.2'
    id "com.github.ben-manes.versions" version "0.44.0"
    id 'org.springframework.boot' version '2.7.5' apply false
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'io.spring.dependency-management'

dependencyManagement {
    imports {
        mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
    }
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

group = 'com.nitrobox'
description = 'KeyValueResolverMongoDBPersistence'
java.sourceCompatibility = 11

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'com.nitrobox:keyvalueresolver:0.28.0'

    implementation 'org.slf4j:slf4j-api:2.0.4'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.assertj:assertj-core:3.22.0'
    testImplementation 'ch.qos.logback:logback-classic:1.2.11'
    testImplementation 'org.mockito:mockito-core:4.3.1'
    testImplementation 'org.mockito:mockito-junit-jupiter:4.3.1'
    testImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

test {
    useJUnitPlatform()
}

reckon {
    scopeFromProp()
    snapshotFromProp()
}

reckonTagCreate.dependsOn check

repositories {
    mavenLocal()
    maven {
        name = "GitHubPackagesRoperty"
        url = uri("https://maven.pkg.github.com/nitrobox/KeyValuesResolver")
        credentials {
            username = project.findProperty("github.user") ?: System.getenv("GITHUB_USER")
            password = project.findProperty("github.token") ?: System.getenv("GITHUB_TOKEN")
        }
    }
    maven {
        url = uri('https://repo.maven.apache.org/maven2/')
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier('sources')
    from sourceSets.main.allSource
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/nitrobox/KeyValueResolverMongoDBPersistence")
            credentials {
                username = project.findProperty("github.user") ?: System.getenv("GITHUB_USER")
                password = project.findProperty("github.token") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier 'sources'
            }
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
